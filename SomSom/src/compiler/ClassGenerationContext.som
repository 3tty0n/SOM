ClassGenerationContext = (
  | universe
    superName
    classSide
    classFields instanceFields
    classMethods instanceMethods |

  initalize: aUniverse = (
    universe := aUniverse.
    classSide := false.
    classFields := Vector new.
    instanceFields := Vector new.
    classMethods := Vector new.
    instanceMethods := Vector new.
  )

  superName = (
    ^ superName
  )

  superName: aSymbol = (
    superName := aSymbol
  )

  addField: aSymbol = (
    classSide
      ifTrue: [classFields append: aSymbol]
      ifFalse: [instanceFields append: aSymbol]
  )

  hasField: aSymbol = (
    ^ classSide
      ifTrue: [classFields contains: aSymbol]
      ifFalse: [instanceFields contains: aSymbol]
  )

  fieldIndex: aSymbol = (
    ^ classSide
      ifTrue: [classFields indexOf: aSymbol]
      ifFalse: [instanceFields indexOf: aSymbol]
  )

  addMethod: anInvokable = (
    classSide
      ifTrue: [classMethods append: anInvokable]
      ifFalse: [instanceMethods append: anInvokable]
  )

  startClassSide = (
    classSide := true
  )

  ----

  new: aUniverse = (
    ^ self new initalize: aUniverse
  )
)
